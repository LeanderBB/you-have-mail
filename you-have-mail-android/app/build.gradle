// NB: Android Studio can't find the imports; this does not affect the
// actual build since Gradle can find them just fine.
import com.android.tools.profgen.ArtProfileKt
import com.android.tools.profgen.ArtProfileSerializer
import com.android.tools.profgen.DexFile
import org.gradle.internal.os.OperatingSystem

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id "org.mozilla.rust-android-gradle.rust-android" version "0.9.6"
    id "org.jetbrains.kotlin.plugin.compose" version '2.3.0'
}

android {
    namespace 'dev.lbeernaert.youhavemail'
    compileSdk = 36

    defaultConfig {
        applicationId "dev.lbeernaert.youhavemail"
        minSdk 26
        targetSdk 36
        versionCode 45
        versionName "0.22.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    ndkVersion '28.2.13676358'

    defaultConfig {
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a', 'x86_64', 'armeabi-v7a'
        }
    }

    // https://gitlab.com/fdroid/fdroiddata/-/issues/3330#note_2189915200
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
}

dependencies {
    // WorkManager dependency
    implementation "androidx.work:work-runtime-ktx:$versions_work"

    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.10.0'
    implementation 'androidx.activity:activity-compose:1.12.3'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation 'androidx.compose.material3:material3:1.4.0'
    implementation 'androidx.security:security-crypto-ktx:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
    implementation 'androidx.navigation:navigation-compose:2.9.7'
    implementation "androidx.compose.material:material-icons-extended:1.7.8"

    // Required for JNI
    implementation 'net.java.dev.jna:jna:5.18.1@aar'
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'


cargo {
    module = "../../you-have-mail-mobile"
    libname = "youhavemail"
    targets = ["x86_64", "arm64", "arm"]
    targetIncludes = ["libyouhavemail.so"]
    targetDirectory = "/tmp/build-yhm"
    profile = "release"
    prebuiltToolchains = true
    rustupChannel = "1.88.0"
    apiLevel = 26

    // Ensure paths are consistent for reproducible builds.
    exec { spec, _ ->
        var home = "$System.env.HOME"
        var pwd = "$System.env.PWD"
        spec.environment("RUSTFLAGS", "--remap-path-prefix=${home}=/remap --remap-path-prefix=${pwd}=/remap --remap-path-prefix=${pwd}/you-have-mail=/remap")
        spec.environment("CARGO_TARGET_DIR", "/tmp/build-yhm")
    }

    extraCargoBuildArguments = ['--locked']
}

task bindingBuild(type: Exec) {
    workingDir "../../you-have-mail-mobile"
    commandLine "cargo", "build"
}

var ext = "so"
if (OperatingSystem.current().isMacOsX()) {
    ext = "dylib"
}

task genBindings(type: Exec) {
    workingDir "../.."
    commandLine "cargo", "run", "--bin", "uniffi-bindgen", "--", "generate",
            "--no-format", "--library",
            "target/debug/libyouhavemail.$ext",
            "--language", "kotlin", "--config", "uniffi.toml",
            "--out-dir", "you-have-mail-android/app/src/main/java"
}

tasks.whenTaskAdded { task ->
    // Require cargo to be run before copying native libraries.
    if ((task.name == 'mergeDebugJniLibFolders' || task.name == 'mergeReleaseJniLibFolders')) {
        task.dependsOn 'cargoBuild'
    }

    if (task.name == 'preBuild') {
        task.dependsOn genBindings
    }

    if (task.name == 'genBindings') {
        task.dependsOn 'cargoBuild'
    }

    if (task.name == 'compileDebugKotlin') {
        task.dependsOn 'genBindings'
    }
}

afterEvaluate {

    // The `cargoBuild` task isn't available until after evaluation.
    android.applicationVariants.all { variant ->
        def productFlavor = ""
        variant.productFlavors.each {
            productFlavor += "${it.name.capitalize()}"
        }
        def buildType = "${variant.buildType.name.capitalize()}"
        tasks["generate${productFlavor}${buildType}Assets"].dependsOn(tasks["cargoBuild"])
    }
}

// Sort .profm files for reproducible builds. Taken from
// https://gist.github.com/obfusk/eb82a810ed6aad266dab19977b18cee6
project.afterEvaluate {
    tasks.each { task ->
        if (task.name.startsWith("compile") && task.name.endsWith("ReleaseArtProfile")) {
            task.doLast {
                outputs.files.each { file ->
                    if (file.name.endsWith(".profm")) {
                        println("Sorting ${file} ...")
                        def version = ArtProfileSerializer.valueOf("METADATA_0_0_2")
                        def profile = ArtProfileKt.ArtProfile(file)
                        def keys = new ArrayList(profile.profileData.keySet())
                        def sortedData = new LinkedHashMap()
                        Collections.sort keys, new DexFile.Companion()
                        keys.each { key -> sortedData[key] = profile.profileData[key] }
                        new FileOutputStream(file).with {
                            write(version.magicBytes$profgen)
                            write(version.versionBytes$profgen)
                            version.write$profgen(it, sortedData, "")
                        }
                    }
                }
            }
        }
    }
}

afterEvaluate {
    genBindings.dependsOn(bindingBuild, cargoBuild)
    android.applicationVariants.all { variant ->
        variant.javaCompileProvider.configure {
            dependsOn(genBindings)
        }
    }
}
